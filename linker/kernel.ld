ENTRY(boot)

SECTIONS {
    /* ロケーションカウンタの設定(現在のアドレスでココの番地からメモリを使う) */
    . = 0x80200000; 

    /* TEXTセクションの指定 */
    .text :{
        /* textセクションのbootラベルを最適化で消されないようにする */
        /* bootラベルはkernelからは呼ばれていないため最適化の対象になる */
        KEEP(*(.text.boot)); 
        /* 全ファイル中(*)の.textと.text.で始まるセクションを配置する */
        *(.text .text.*);
    }

    /* constで宣言された書き換え不可のデータを扱うセクション */
    /* 4バイト境界になるようにアドレスを調節する(riscv32だから?) */
    .rodata : ALIGN(4) {
        *(.rodata .rodata.*);
    }

    /* 初期値を持つグローバル変数やstatic変数を配置するセクション */
    .data : ALIGN(4) {
        *(.data .data.*);
    }

    /* 初期値を持たないグローバル変数やstatic変数を配置するセクション */
    .bss : ALIGN(4) {
        /* __bssというシンボルに現在のアドレスを割り当てる */
        /* extern char シンボル名 で定義したシンボルを参照できる */
        __bss = .;
        *(.bss .bss.* .sbss .sbss.*);
        __bss_end = .;
    }

    . = ALIGN(4);
    /* ブート時にスタックを128KB使用するためにスタック領域を確保する */
    . += 128 * 1024;
    __stack_top = .;

    /* 動的割り当てを行える領域 */
    /* ページサイズ4KiBでアラインメントしないとSv32だとpage-faultが発生する */
    . = ALIGN(4096);
    __free_ram = .;
    . += 128 * 1024 * 1024; /* 128MiB (qemuでオプション-mで指定する) */
    __free_ram_end = .;
}